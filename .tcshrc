#!tcsh
alias warn 'echo >/dev/stderr'

if ( ! $?SampShell_ROOTDIR ) then
	setenv SampShell_ROOTDIR "$HOME/sampshell"
endif

source "$SampShell_ROOTDIR"/.cshrc

if ( $?prompt && $?tcsh ) then # only do load up this file if a prompt is defined
	alias cd        'pushd'
	alias cdd       'cd `dirname \!*`'

	alias reload    'source ~/.tcshrc'
	alias md        'mkdir -p \!*:q && \cd -- \!*q'
	alias ls        'ls -AFqhGb'
	alias s         'subl'
	alias ss        'subl --create'
	alias cdss      'cd $SampShell_ROOTDIR:q'

	set cdpath = ( ~/.cdpath )
	# TODO, look at highlight
	set globstar noclobber printexitvalue rmstar autolist correct \
		dunique highlight pushdsilent


	set shell       = /bin/sh
	set symlinks    = chase
	set echo_style  = both
	set complete    = Enhance
	set histchars   = '\!,'
	set histdup     = prev
	set history     = 1000
	set savehist    = ( 10000 merge lock )
	set histfile    = "$XDG_STATE_HOME"/tcsh/history
	mkdir -p "$XDG_STATE_HOME"/tcsh
	set ignoreeof   = 2
	set listflags   = A color

	unset promptchars # MACOS SETS THIS FOR SOME REASON
	set prompt                    = '%S%B[%b%p \\!%U\!%u ?%?'
	if ( $SHLVL != 1 ) set prompt = "$prompt L%"\$SHLVL
	set prompt                    = "$prompt%B]%b %c03 %n%B%#%b%s "


	# Ignore `>`, eg from the examples
	alias '>' = ''

	alias helpcommand '\!:1 --help'
	## OTHER STUFF I HAVENT FIGURE DOUT IF I WANT
	# # alias precmd date
	# # set backslash_quote <-- let's live dangerouslynadnot set it
	# set color
	# set correct = cmd
	# # set ellipsis
	#               Under very rare circumstances, the shell may report `You have mail.' instead of `You have new mail.'

	alias h 'history \!:* 16 | awk '\''  \\
	{ a[NR] = $0 }                       \\
	END {                                \\
	   x = $1 + 1                        \\
	   sep = "%" length(-x) "d %s\n"     \\
	   for (i = 1; i < NR; i++) {        \\
	      $0 = a[i]                      \\
	      printf sep, $1-x, a[i]         \\
	   }                                 \\
	}'\'

	# aliases based on git aliases
	foreach tmp ( `git config -f $SampShell_ROOTDIR/.gitconfig --get-regexp '^alias\.' | grep -v '^alias\.[a-z]*-' | sed 's/^alias\.\([a-z]*\).*/\1/'` )
		alias g$tmp "git $tmp"
	end
	unset tmp

	alias g   'git'
	alias gls 'git log-short'
endif
