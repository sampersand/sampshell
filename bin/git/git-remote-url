#!/bin/zsh

##
# Print the url for a repo, or a file in the repo
##

set -eu

zparseopts -D -F -Aopts \
	b: \
	h

if (( $+opts[-h] || $# > 3 )) then
	echo "usage: ${0:t:s/-/ /} [-bbranch] [file] [start[-end]]" >&2
	exit 80
fi

(( $# >= 1 )) && cd -- $1:h

repo=$(gh repo view --json=url --jq=.url)
branch=${opts[-b]:-$(git rev-parse --abbrev-ref HEAD)}
file=''

if (( $# >= 1 )) then
	# Strip the repo's directory, if it's present
	file=${${1#$(git rev-parse --show-toplevel)}#/}

	# Add line number, if it's specified
	if (( $# == 2 )) then
		file+="#L${2/-/-L}"
	fi
fi


print -r -- $repo/blob/$branch${file:+/}$file
