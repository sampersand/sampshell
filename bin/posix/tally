#!/usr/bin/awk -f

###
# Tallies the amount of times line sin the input appear.
#
# You can supply `-vwidth=<NUM>` to explicitly set the width; without this, the
# width is automatically ascertained from the max length of all fields
##

# Accumulate the counts, and also set the maximum length
max < ++counts[$0] {
  max = counts[$0]
}

END {
  # Create the format string, using the length of the max number as the padding.
  fmt = "%-" (width ? width : length(max)) "d %s\n"

  # Print out the lines, and pipe it to `sort`.
  for (line in counts) {
    printf fmt, counts[line], line | "LC_ALL=C sort -n -k1"
  }
}
