#!/bin/tcsh -f

## This file's a shim around `bc` to convert between different bases. While it
# does work, it's really just me having fun with tcsh, as this can be much more
# easily done in sh/zsh.
##

alias die 'echo \!* > /dev/stderr ; exit 1'

set argv = ( `getopt hxXbBi:o: $argv` )
set input = 10
set output = 10

while ( $# )
   set opt = $1; shift
   switch ( $opt )
   case -h:
      goto help
   case "-x":
      set input = 16; breaksw
   case -X:
      set output = 16; breaksw
   case -b:
      set input = 2; breaksw
   case -B:
      set output = 2; breaksw
   case -i:
      set input = $1; shift; breaksw
   case -o:
      set output = $1; shift; breaksw
   case "--":
      break
   endsw
end
set argv = ( $input $output $argv )
if ( $#argv < 3 ) then
help:
   die "usage: $0:t [-x | -b | -i input] [-X | -B | -o output] numbers [...]\
(you can also aliases for bases: x=16, o=8, d=10, b=2.)"
endif

alias convert 'eval "              \\
switch ( $\!:1 )                   \\
case x:                            \\
   set \!:2base = 16               \\
   breaksw                         \\
case o:                            \\
   set \!:2base = 8                \\
   breaksw                         \\
case d:                            \\
   set \!:2base = 10               \\
   breaksw                         \\
case b:                            \\
   set \!:2base = 2                \\
   breaksw                         \\
case "\"\"":                       \\
case *[^0-9]*:                     \\
   die argument \#\!:1 is not a   \\\
      number or xodb               \\
default:                           \\
   set \!:2base = $\!:1            \\
endsw"'

convert 1 i
convert 2 o

shift; shift
bc <<EOS
obase=$obase
ibase=$ibase
`echo "$argv" | tr 'a-z ' 'A-Z\n'`
