#!/usr/bin/env -S tcsh -fe

set outputfile = `mktemp`
set exitstatus = 0
onintr rmout

if ( $#argv == 0 ) then
	echo "usage: $0:t (<main's body> | - <entire contents>) [flags to cc]" >/dev/stderr
	set exitstatus = 1
	goto rmout
else if ( '-' == "$1" ) then
	set body = "$2"
	shift
	shift
else
	set body = "int main(int argc, char **argv) { $1:q; }"
	shift
endif

cc -x c -o $outputfile:q - $argv:q <<EOS
#include <stdio.h>
#include <stdlib.h>
#include <ctype.h>
#include <string.h>
#include <limits.h>
$body
EOS

$outputfile
set exitstatus = $status

rmout:
	rm -f "$outputfile"
	exit $exitstatus
