#!zsh

## This has been supplanted by using `zle/deltochar`

## Deletes from the cursor to a given character, either forwards or backwards.
# NOTE: there's a builtin module `zmodload zle/deltochar` which exposes the
# widget `delete-to-char` which does something very similar, except it by goes
# the other direction by default.

# Read a single character and put it into `$chr`
read -sk chr

# If `chr` doesn't exist in the string, "beep" and return.
(( BUFFER[(I)$chr] )) || { zle beep; return }

# The amount of repetitions. If it's negative, we delete forwards.
# (NOTE: You can specify repetitions by doing `ESC` then a digit before hand, eg
# "ESC, `2`, ESC, `=`, `e`" will delete to the second closest `e`.)
amount=${NUMERIC:-1}

# Remember stuff before and after the cursor, so we can combine them later.
post=${BUFFER:$CURSOR}
pre=${BUFFER::$CURSOR}

# Delete forwards or backwards depending on the sign of `amount`
if (( amount < 0 )) then
	while (( amount++ )) do post=${post#*$chr} done
else
	while (( amount-- )) do pre=${pre%$chr*} done
fi

CURSOR=$#pre     # Set the cursor's position to the amount of "pre" characters remaining
BUFFER=$pre$post # Rebuild the buffer from stuff before and after the cursor.
