#!zsh

#### Basic SampShell definitions for _all_ ZSH shell instances.
# While this file is usually `.`d from the top-level `env.sh` file, it can be `.`d on its own.
#
# Since this file is going to be `source`d for _every_ zsh instance, it should be kept short. Not
# only will this make it faster to load (which speeds up execution of scripts), but also keeps our
# config minimal. Again, this file is `source`d` for EVERY SINGLE ZSH INSTANCE, including scripts
# that we didn't write, so we don't want to do anything that's potentially disruptive!
#
# Note: This file intentionally doesn't start with a `.`, as it's not meant to be used directly as
# a user's `.zshenv`. (Instead, `source` the top-level `env.sh` file if needed.)
####

####################################################################################################
#                                  Enable Profiling if Requested                                   #
####################################################################################################

## If the `$SampShell_PROFILE` variable is defined and nonempty, then we enable profiling.
# (We eval it so we doesn't parse it if it's not profiling)
[[ -n ${SampShell_PROFILE:-} ]] && eval '
zshexit_functions+=(_SampShell_profile_exit)
function _SampShell_profile_exit { zprof }
zmodload zsh/zprof'

####################################################################################################
#                                         Set Debug Prompt                                         #
####################################################################################################

# Set the debug prompt to something a bit more informative. I'm still not sure how much I like this.
[[ -n $SampShell_no_experimental ]] && PS4='+%x:%N:%I> '

## Set sourcetrace prompt (temporary)
[[ -o sourcetrace && -n ${SampShell_SOURCETRACE} ]] && {
	typeset -Fg SECONDS
	setopt promptsubst
	PS4='+$SECONDS:%x:%I> '
}

####################################################################################################
#                                           Setup $PATH                                            #
####################################################################################################

typeset -xgU path  # Ensure `path` is unique, and export it (in case it wasn't already).

####################################################################################################
#                                       SampShell_{un,}debug                                       #
####################################################################################################

## Re-define the `SampShell_debug` and `SampShell_undebug` functions that were previously declared
# in `posix/env.sh`. Because ZSH has more debugging options, we want to make sure we use the same
# name so that POSIX-compliant scripts run under ZSH will automatically use the enhanced debug fns.
#
# ZSH is annoying, however, as `setopt XTRACE` only lasts until the end of the function it's set in,
# and there's literally no way to change that. The best "solution" I have found is to set an EXIT
# trap, which are run within the calling function's context, and in the trap set the XTRACE option.
# Not great, especially since when the calling function returns `XTRACE` is unset :-/. Alas, ZSH.
# (We actually set all the options in the trap, as we `setopt LOCAL_OPTIONS` so we can have local
# traps.)
#
# Note that, just like the POSIX versions of these, these will not restore the options in their
# surrounding environment when `undebug` is called. While I could maybe do that one day (eg by using
# a stack for old options or something), I haven't used the `debug`/`undebug` functions enough to
# warrant delving into that. Possible future TODO?
function SampShell_debug {
	setopt LOCAL_OPTIONS LOCAL_TRAPS
	export SampShell_TRACE=1
	trap 'setopt XTRACE VERBOSE WARN_CREATE_GLOBAL WARN_NESTED_VAR' EXIT
}

function SampShell_undebug {
	setopt LOCAL_OPTIONS LOCAL_TRAPS
	unset SampShell_TRACE
	trap 'unsetopt XTRACE VERBOSE WARN_CREATE_GLOBAL WARN_NESTED_VAR' EXIT
}

####################################################################################################
#                                     Respect SampShell_TRACE                                      #
####################################################################################################

## Respect `SampShell_TRACE` in all scripts, regardless of whether they're a SampShell script.
# We want this as the last thing in this file, so we don't print traces for the other setup config.
if [[ -n $SampShell_TRACE ]]; then
	export SampShell_TRACE # in case it's not already exported for some reason
	setopt SOURCE_TRACE XTRACE
fi

