#!zsh

##
# Add a directory to the list of named directories.
# (This file is really just a wrapper around `hash -d` with some error handling)
##

emulate -L zsh -o EXTENDED_GLOB

if (( $# == 0 )) then
	print >&2 "usage: $0 [name=]dir [...]"
	print >&2
	print >&2 'Lets you use ~name as a shorthand for `dir`, eg `cd ~name/bar/baz`'
	print >&2 'If `name` is not given, it defaults to the last part of `dir`'
	return 1
fi

local arg
local error=0

for arg do
	if [[ -z $arg ]] then
		print >&2 -r -- "$0: empty argument ignored"; error=1
	elif [[ $arg = \=* ]] then
		print >&2 -r -- "$0: an empty name was given: ${(q)arg}"; error=1
	elif [[ $arg = [-._[:alnum:]]##= ]] then
		print >&2 -r -- "$0: an empty directory was given: ${(q)arg}"; error=1
	else
		if [[ $arg != [-._[:alnum:]]##=* ]] then
			# To use the default name, the directory must end with only alnums, `_`, or
			# a period. (Anything else won't be a valid directory name, so it'll fail.)
			if [[ $arg != (*/|)[-._[:alnum:]]##(/|) ]]; then
				print >&2 -r -- "$0: can't infer default name for: ${(q)arg}"; error=1
				continue
			fi
			arg=${arg:t}=$arg
		fi

		builtin hash -d -- $arg
	fi
done

return $error
