#!zsh
emulate -L zsh

# If we're not connected to a TTY, the just act like `fc -l`, except all values are
# printed out by default. This lets us do `h | grep ...`
if [[ ! -t 1 ]] then
	fc -l ${@:-0}
	return
fi

# If any arguments were given that aren't integers, instead interpret them as args to `grep`, and
# pass everything to grep.
if [[ $# -ne 0 && $1 != (-|)<-> ]]; then
	fc -l 0 | egrep -i "$@"
	return
fi

# fc -l $@ | awk '
# { a[NR] = $0 }
# END {
# 	x = $1 + 1
# 	sep = "%" length(x) "d %s\n"
# 	for (i = 1; i < NR; i++) {
# 		$0 = a[i]
# 		printf sp, $1-x, a[i]
# 	}
# }'

local sep amount REPLY
# Number the output lines
if [[ $1 = <-> && $1 != -* ]] argv[1]=-$argv[1]

fc -l $@ | while read -r; do
	amount=-$(( HISTCMD - ${REPLY[(wr)<->]} + 1 ))
	printf "%${sep:=$#amount}d %s\\n" $amount $REPLY
done
